apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def siteUrl = 'https://github.com/WaterlooBridge/Packer'
def gitUrl = 'https://github.com/WaterlooBridge/Packer.git'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 14
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
}

group = "com.zhenl.support"
archivesBaseName = "packer"
version = "1.0.2"

task androidJavadocs(type: Javadoc) {
    failOnError = false
    source = android.sourceSets.main.java.srcDirs
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath += files(ext.androidJar)
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "Packer"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

task makeJar(dependsOn: build) {
    doLast {
        exec {
            commandLine "jar", "cvfm", "./build/packer.jar", "./MANIFEST.MF", "-C", "./build/intermediates/javac/release/compileReleaseJavaWithJavac/classes", "."
        }
    }
}